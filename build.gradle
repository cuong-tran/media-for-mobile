// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "com.jfrog.artifactory" version "4.0.0"
}

allprojects {
    repositories {
        jcenter()
        maven {
            url getBuildProperty("ARTIFACTORY_CONTEXT_URL") + "/" + getBuildProperty("ARTIFACTORY_REPO_KEY")
            credentials {
                username getBuildProperty("ARTIFACTORY_USERNAME")
                password getBuildProperty("ARTIFACTORY_PASSWORD")
            }
        }
        maven {
            url getBuildProperty("ARTIFACTORY_CONTEXT_URL") + "/" + getBuildProperty("ARTIFACTORY_SNAPSHOTS_REPO_KEY")
            credentials {
                username getBuildProperty("ARTIFACTORY_USERNAME")
                password getBuildProperty("ARTIFACTORY_PASSWORD")
            }
        }
    }
    afterEvaluate { project ->
        if (project.hasProperty("android")) {
            project.android.compileOptions.incremental = true
            project.ext.configsPath = "${project.rootDir}/misc"
        }
    }
}

task wrapper(type: Wrapper) {
      gradleVersion = 3.1
//      distributionType = Wrapper.DistributionType.ALL
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

Object getBuildProperty(String varName, String defaultValue = null) {
    if (System.getenv().containsKey(varName)) {
        return System.getenv()[varName]
    }

    def File propsFile = file(project.rootProject.file('local.properties'))
    if (propsFile.exists() && propsFile.canRead()) {
        def Properties localProps = new Properties()

        localProps.load(new FileInputStream(propsFile))
        if (localProps.containsKey(varName)) {
            return localProps[varName];
        }
    }

    if (rootProject.hasProperty(varName)) {
        return rootProject.property(varName)
    }

    return defaultValue
}

task copyJarDomain(type: Copy) {
    from('domain/build/libs/domain.jar')
    into('release/')
}

//task copyAARAndroid(type: Copy) {
//    from('android/build/outputs/aar/android-release.aar')
//    into('release/')
//}

task copyJarAspectjrt(type: Copy) {
    from('m4m/libs/aspectjrt-1.7.3.jar')
    into('release/')
}

task copyJarIsoParser(type: Copy) {
    from('m4m/libs/isoparser-1.0.2.jar')
    into('release/')
}

task copyJarAndroid(type: Copy) {
    from('android/build/intermediates/bundles/default/classes.jar')
    into('release/')
    rename('classes.jar', 'android.jar')
}

task copyJarEffects(type: Copy) {
    from('effects/build/intermediates/bundles/default/classes.jar')
    into('release/')
    rename('classes.jar', 'effects.jar')
}

task copyJarM4M(type: Copy) {
    from('m4m/build/intermediates/bundles/default/classes.jar')
    into('release/')
    rename('classes.jar', 'm4m.jar')
}

task deleteOldJars(type: Delete) << {
    delete 'release/m4m.jar'
    delete 'release/domain.jar'
    delete 'release/effects.jar'
    delete 'release/android.jar'
    delete 'release/android-release.aar'
}

task exportJars() {}
exportJars.dependsOn(deleteOldJars, copyJarDomain, copyJarEffects, copyJarM4M, copyJarAndroid, copyJarIsoParser, copyJarAspectjrt)
